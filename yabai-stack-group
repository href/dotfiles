#!/usr/local/bin/elvish
#
# Automatically stack certain applications on top of each other. Expects to
# find YABAI_WINDOW_ID in the environment.
#
# Usage: yabai-stack-group "First App, Second App, Third App"
use str

fn int {|n|
  to-string (exact-num $n)
}

# The window that was just created
var new-window = (yabai -m query --windows ^
  | jq '.[] | select(.id == '$E:YABAI_WINDOW_ID')' ^
  | from-json)

# The group we are looking for
var group = [(put (str:split ',' $args[0]) | each {|a| str:trim $a ' '})]

# Exit early if the window does not belong to the group
if (not (has-value $group $new-window[app])) {
  exit 0
}

# All the windows
var windows = [(yabai -m query --windows | from-json)][0]

# If there is another window of this group, stack the new window on top
for window $windows {
  if (eq $new-window[id] $window[id]) {
    continue
  }
  
  if (has-value $group $window[app]) {
    yabai -m window (int $new-window[id]) --stack (int $window[id])
    break 
  }
}

# Otherwise, there's nothing to be done, launch normally.
exit 0